/*
 * Copyright 2019 Red Hat, Inc. and/or its affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.optaweb.employeerostering.service.solver;
    dialect "java"

import java.time.temporal.ChronoUnit;
import java.time.DayOfWeek;
import java.time.Duration;
import java.time.temporal.IsoFields;
import java.util.Collection;
import java.util.Map;
import java.util.HashMap;

import org.optaplanner.core.api.score.buildin.hardmediumsoftlong.HardMediumSoftLongScoreHolder;
import org.optaweb.employeerostering.domain.employee.CovidRiskType;
import org.optaweb.employeerostering.domain.employee.Employee;
import org.optaweb.employeerostering.domain.employee.EmployeeAvailability;
import org.optaweb.employeerostering.domain.employee.EmployeeAvailabilityState;
import org.optaweb.employeerostering.domain.shift.Shift;
import org.optaweb.employeerostering.domain.skill.Skill;
import org.optaweb.employeerostering.domain.roster.Roster;
import org.optaweb.employeerostering.domain.spot.Spot;
import org.optaweb.employeerostering.domain.tenant.RosterConstraintConfiguration;
import org.optaweb.employeerostering.domain.common.DateTimeUtils;

global HardMediumSoftLongScoreHolder scoreHolder;

// ############################################################################
// COVID-19 constraints
// ############################################################################

rule "Low-risk employee assigned to a COVID ward"
    when
        $shift: Shift(employee != null)
        $employee: Employee(this == $shift.employee, covidRiskType == CovidRiskType.LOW)
        $ward: Spot(this == $shift.spot, isCovidWard() == true)
    then
        scoreHolder.penalize(kcontext);
end

rule "Moderate-risk employee assigned to a COVID ward"
    when
        $shift: Shift(employee != null)
        $employee: Employee(this == $shift.employee, covidRiskType == CovidRiskType.MODERATE)
        $ward: Spot(this == $shift.spot, isCovidWard() == true)
    then
        scoreHolder.penalize(kcontext);
end

rule "High-risk employee assigned to a COVID ward"
    when
        $shift: Shift(employee != null)
        $employee: Employee(this == $shift.employee, covidRiskType == CovidRiskType.HIGH)
        $ward: Spot(this == $shift.spot, isCovidWard() == true)
    then
        scoreHolder.penalize(kcontext);
end

rule "Extreme-risk employee assigned to a COVID ward"
    when
        $shift: Shift(employee != null)
        $employee: Employee(this == $shift.employee, covidRiskType == CovidRiskType.EXTREME)
        $ward: Spot(this == $shift.spot, isCovidWard() == true)
    then
        scoreHolder.penalize(kcontext);
end

// PLANNER-1925 1
rule "Inoculated employee outside a COVID ward"
    when
        $shift: Shift(employee != null, spot.isCovidWard() == false)
        $employee: Employee(this == $shift.employee, covidRiskType == CovidRiskType.INOCULATED)
    then
        scoreHolder.penalize(kcontext);
end

// PLANNER-1925 2
rule "Uniform distribution of inoculated hours"
    when
        $allCounts: Collection( size > 0 ) from accumulate(
            $shift: Shift(employee != null, employee.covidRiskType == CovidRiskType.INOCULATED, spot.isCovidWard() == true),
            init(
                Map hours = new HashMap();
            ), action(
                $shift.increaseHourlyCounts(hours);
            ), reverse(
                $shift.decreaseHourlyCounts(hours);
            ), result(
                hours.values()
            )
        )
    then
        scoreHolder.penalize(kcontext, Shift.calculateLoad($allCounts));
end

// PLANNER-1925 3, to prevent load balancing from setting loads to zero
rule "Maximize inoculated hours"
    when
        $shift: Shift(employee != null, spot.isCovidWard() == true)
        $employee: Employee(this == $shift.employee, covidRiskType == CovidRiskType.INOCULATED)
    then
        scoreHolder.reward(kcontext, $shift.getLength(ChronoUnit.HOURS));
end

// PLANNER-1926 A
rule "Migration between COVID and non-COVID wards"
    when
        $employee: Employee()
        $covidShift: Shift(employee == $employee, spot.isCovidWard() == true)
        $nonCovidShift: Shift(employee == $employee, this != $covidShift,
            spot.isCovidWard() == false,
            follows($covidShift))
        not Shift(employee == $employee, this != $covidShift, this != $nonCovidShift,
            follows($covidShift),
            precedes($nonCovidShift))
    then
        scoreHolder.penalize(kcontext);
end

// PLANNER-1926 C
rule "Non-COVID shift started less than 8 hours after finishing a COVID shift"
    when
        $employee: Employee()
        $nonCovidShift: Shift(employee == $employee, spot.isCovidWard() == false)
        $covidShift: Shift(employee == $employee, this != $covidShift,
            spot.isCovidWard() == true,
            precedes($nonCovidShift),
            endDateTime.until($nonCovidShift.startDateTime, ChronoUnit.HOURS) < 8)
        not Shift(employee == $employee, this != $covidShift, this != $nonCovidShift,
            follows($covidShift),
            precedes($nonCovidShift))
    then
        scoreHolder.penalize(kcontext);
end

// ############################################################################
// Hard constraints
// ############################################################################

rule "Required skill for a shift"
    when
        Shift(
                employee != null,
                !getEmployee().hasSkills(getSpot().getRequiredSkillSet()))
    then
        scoreHolder.penalize(kcontext);
end

rule "Unavailable time slot for an employee"
    when
        EmployeeAvailability(
                state == EmployeeAvailabilityState.UNAVAILABLE,
                $e : employee,
                $startDateTime : startDateTime,
                $endDateTime : endDateTime)
        Shift(
                employee == $e,
                DateTimeUtils.doTimeslotsIntersect($startDateTime,$endDateTime,
                                                   startDateTime, endDateTime))
    then
        scoreHolder.penalize(kcontext);
end

rule "At most one shift assignment per day per employee"
    when
        $s : Shift(
                employee != null,
                $e : employee,
                $leftDay : startDateTime.toLocalDate())
        Shift(
                employee == $e,
                startDateTime.toLocalDate() == $leftDay,
                this != $s)
    then
        scoreHolder.penalize(kcontext);
end

rule "No 2 shifts within 10 hours from each other"
    when
        $s : Shift(
                employee != null,
                $e : employee,
                $leftEndDateTime : endDateTime)
        Shift(
                employee == $e,
                $leftEndDateTime <= endDateTime,
                $leftEndDateTime.until(startDateTime, ChronoUnit.HOURS) < 10,
                this != $s)
    then
        scoreHolder.penalize(kcontext);
end

rule "Daily minutes must not exceed contract maximum"
    when
        $employee : Employee($contract : contract, $contract.getMaximumMinutesPerDay() != null)
        $s : Shift(employee == $employee, $startDateTime : startDateTime)
        Number( intValue > $contract.getMaximumMinutesPerDay() ) from accumulate(
            Shift(employee == $employee, $shiftStart : startDateTime,
            $shiftEnd : endDateTime,
            $shiftStart.toLocalDate().equals($startDateTime.toLocalDate())),
            sum(Duration.between($shiftStart, $shiftEnd).toMinutes())
        )
    then
        scoreHolder.penalize(kcontext);
end

rule "Weekly minutes must not exceed contract maximum"
    when
        $rosterConstraintConfiguration : RosterConstraintConfiguration()
        $employee : Employee($contract : contract, $contract.getMaximumMinutesPerWeek() != null)
        $s : Shift(employee == $employee, $startDateTime : startDateTime)
        Number( intValue > $contract.getMaximumMinutesPerWeek() ) from accumulate(
            Shift(employee == $employee, $shiftStart : startDateTime,
            $shiftEnd : endDateTime,
            DateTimeUtils.sameWeek($rosterConstraintConfiguration.getWeekStartDay(), $shiftStart, $startDateTime)),
            sum(Duration.between($shiftStart, $shiftEnd).toMinutes())
        )
    then
        scoreHolder.penalize(kcontext);
end

rule "Monthly minutes must not exceed contract maximum"
    when
        $employee : Employee($contract : contract, $contract.getMaximumMinutesPerMonth() != null)
        $s : Shift(employee == $employee, $startDateTime : startDateTime)
        Number( intValue > $contract.getMaximumMinutesPerMonth() ) from accumulate(
            Shift(employee == $employee, $shiftStart : startDateTime,
            $shiftEnd : endDateTime,
            $shiftStart.getMonth() == $startDateTime.getMonth(),
            $shiftStart.getYear() == $startDateTime.getYear()),
            sum(Duration.between($shiftStart, $shiftEnd).toMinutes())
        )
    then
        scoreHolder.penalize(kcontext);
end

rule "Yearly minutes must not exceed contract maximum"
    when
        $employee : Employee($contract : contract, $contract.getMaximumMinutesPerYear() != null)
        $s : Shift(employee == $employee, $startDateTime : startDateTime)
        Number( intValue > $contract.getMaximumMinutesPerYear() ) from accumulate(
            Shift(employee == $employee, $shiftStart : startDateTime,
            $shiftEnd : endDateTime,
            $shiftStart.getYear() == $startDateTime.getYear()),
            sum(Duration.between($shiftStart, $shiftEnd).toMinutes())
        )
    then
        scoreHolder.penalize(kcontext);
end


// ############################################################################
// Medium constraints
// ############################################################################

rule "Assign every shift"
    when
        Shift(employee == null)
    then
        scoreHolder.penalize(kcontext);
end


// ############################################################################
// Soft constraints
// ############################################################################

rule "Undesired time slot for an employee"
    when
    	$rosterConstraintConfiguration : RosterConstraintConfiguration(undesiredTimeSlotWeight != 0)
        EmployeeAvailability(
                state == EmployeeAvailabilityState.UNDESIRED,
                $e : employee,
                $startDateTime : startDateTime,
                $endDateTime : endDateTime)
        Shift(
                employee == $e,
                DateTimeUtils.doTimeslotsIntersect($startDateTime,$endDateTime,
                                                   startDateTime, endDateTime))
    then
        scoreHolder.penalize(kcontext, $rosterConstraintConfiguration.getUndesiredTimeSlotWeight());
end

rule "Desired time slot for an employee"
    when
        $rosterConstraintConfiguration : RosterConstraintConfiguration(desiredTimeSlotWeight != 0)
        EmployeeAvailability(
                state == EmployeeAvailabilityState.DESIRED,
                $e : employee,
                $startDateTime : startDateTime,
                $endDateTime : endDateTime)
        Shift(
                employee == $e,
                DateTimeUtils.doTimeslotsIntersect($startDateTime,$endDateTime,
                                                   startDateTime, endDateTime))
    then
        scoreHolder.reward(kcontext, $rosterConstraintConfiguration.getDesiredTimeSlotWeight());
end

rule "Employee is not rotation employee"
    when
    	$rosterConstraintConfiguration : RosterConstraintConfiguration(rotationEmployeeMatchWeight != 0)
        Shift(
                rotationEmployee != null, employee != null, employee != rotationEmployee)
    then
        scoreHolder.penalize(kcontext, $rosterConstraintConfiguration.getRotationEmployeeMatchWeight());
end
